// -*- Metal -*-
//===-- metal_type_traits -------------------------------------------------===//
// Copyright (c) 2017-2021 Apple Inc. All rights reserved
//===----------------------------------------------------------------------===//

#ifndef __METAL_TYPE_TRAITS
#define __METAL_TYPE_TRAITS

#pragma METAL internals : enable
namespace metal
{
template <bool B, typename T = void>
struct enable_if
{
};
template <typename T>
struct enable_if<true, T>
{
  typedef T type;
};
template <bool B, typename T = void>
using enable_if_t = typename enable_if<B, T>::type;

template <bool B, typename T, typename F>
struct conditional
{
  typedef T type;
};
template <typename T, typename F>
struct conditional<false, T, F>
{
  typedef F type;
};
template <bool B, typename T, typename F>
using conditional_t = typename conditional<B, T, F>::type;

template <typename T>
struct remove_volatile
{
  typedef T type;
};
template <typename T>
struct remove_volatile<volatile T>
{
  typedef T type;
};
template <typename T>
using remove_volatile_t = typename remove_volatile<T>::type;

template <typename T>
struct remove_const
{
  typedef T type;
};
template <typename T>
struct remove_const<const T>
{
  typedef T type;
};
template <typename T>
using remove_const_t = typename remove_const<T>::type;

template <typename T>
struct remove_cv
{
  typedef remove_volatile_t<remove_const_t<T>> type;
};
template <typename T>
using remove_cv_t = typename remove_cv<T>::type;

template <typename T>
struct remove_reference
{
  typedef T type;
};
template <typename T>
struct remove_reference<thread T &>
{
  typedef thread T type;
};
template <typename T>
struct remove_reference<thread T &&>
{
  typedef thread T type;
};
template <typename T>
struct remove_reference<device T &>
{
  typedef device T type;
};
template <typename T>
struct remove_reference<device T &&>
{
  typedef device T type;
};
template <typename T>
struct remove_reference<constant T &>
{
  typedef constant T type;
};
template <typename T>
struct remove_reference<constant T &&>
{
  typedef constant T type;
};
template <typename T>
struct remove_reference<threadgroup T &>
{
  typedef threadgroup T type;
};
template <typename T>
struct remove_reference<threadgroup T &&>
{
  typedef threadgroup T type;
};
#if defined(__HAVE_IMAGEBLOCKS__)
template <typename T>
struct remove_reference<threadgroup_imageblock T &>
{
  typedef threadgroup_imageblock T type;
};
template <typename T>
struct remove_reference<threadgroup_imageblock T &&>
{
  typedef threadgroup_imageblock T type;
};
#endif
#if defined(__HAVE_RAYTRACING__)
template <typename T>
struct remove_reference<ray_data T &>
{
  typedef ray_data T type;
};
template <typename T>
struct remove_reference<ray_data T &&>
{
  typedef ray_data T type;
};
#endif
#if defined(__HAVE_MESH__)
template <typename T>
struct remove_reference<object_data T &>
{
  typedef object_data T type;
};
template <typename T>
struct remove_reference<object_data T &&>
{
  typedef object_data T type;
};
#endif
template <typename T>
using remove_reference_t = typename remove_reference<T>::type;

template <typename T, T v>
struct integral_constant
{
  typedef T value_type;
  typedef integral_constant type;
  static constant constexpr T value = v;
};

template <bool B>
using bool_constant = integral_constant<bool, B>;
using true_type = bool_constant<true>;
using false_type = bool_constant<false>;

template <typename T>
struct _is_integral_impl : bool_constant<__is_integral(T)>
{
};
template <typename T, int N>
struct _is_integral_impl<T __attribute__((ext_vector_type(N)))> : _is_integral_impl<T>
{
};
template <typename T, int N>
struct _is_integral_impl<T __attribute__((packed_vector_type(N)))> : _is_integral_impl<T>
{
};
template <typename T>
struct is_integral : _is_integral_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_integral_v = is_integral<T>::value;
#endif

template <typename T>
struct _is_floating_point_impl : bool_constant<__is_floating_point(T)>
{
};
template <typename T, int N>
struct _is_floating_point_impl<T __attribute__((ext_vector_type(N)))> : _is_floating_point_impl<T>
{
};
template <typename T, int N>
struct _is_floating_point_impl<T __attribute__((packed_vector_type(N)))> : _is_floating_point_impl<T>
{
};
template <typename T>
struct is_floating_point : _is_floating_point_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_floating_point_v = is_floating_point<T>::value;
#endif

template <typename T>
struct _is_scalar_impl : bool_constant<__is_scalar(T)>
{
};
template <typename T>
struct is_scalar : _is_scalar_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_scalar_v = is_scalar<T>::value;
#endif

template <typename T>
struct _is_vector_impl : false_type
{
};
template <typename T, int N>
struct _is_vector_impl<T __attribute__((ext_vector_type(N)))> : true_type
{
};
template <typename T, int N>
struct _is_vector_impl<T __attribute__((packed_vector_type(N)))> : true_type
{
};
template <typename T>
struct is_vector : _is_vector_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_vector_v = is_vector<T>::value;
#endif

template <typename T>
struct _is_arithmetic_impl : bool_constant<__is_arithmetic(T)>
{
};
template <typename T, int N>
struct _is_arithmetic_impl<T __attribute__((ext_vector_type(N)))> : _is_arithmetic_impl<T>
{
};
template <typename T, int N>
struct _is_arithmetic_impl<T __attribute__((packed_vector_type(N)))> : _is_arithmetic_impl<T>
{
};
template <typename T>
struct is_arithmetic : _is_arithmetic_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_arithmetic_v = is_arithmetic<T>::value;
#endif

template <typename U, typename V>
struct is_convertible : bool_constant<__is_convertible_to(U, V)>
{
};
#ifdef __cpp_variable_templates
template <typename U, typename V>
constexpr constant bool is_convertible_v = is_convertible<U, V>::value;
#endif

template <typename U, typename V>
struct is_same : bool_constant<__is_same(U, V)>
{
};
#ifdef __cpp_variable_templates
template <typename U, typename V>
constexpr constant bool is_same_v = is_same<U, V>::value;
#endif

template <typename T>
struct is_class : bool_constant<__is_class(T)>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_class_v = is_class<T>::value;
#endif

template <typename T>
struct is_signed : bool_constant<__is_signed(T)>
{
};
template <typename T, int N>
struct is_signed<T __attribute__((ext_vector_type(N)))> : is_signed<T>
{
};
template <typename T, int N>
struct is_signed<T __attribute__((packed_vector_type(N)))> : is_signed<T>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_signed_v = is_signed<T>::value;
#endif

template <typename T>
struct is_unsigned : bool_constant<__is_unsigned(T)>
{
};
template <typename T, int N>
struct is_unsigned<T __attribute__((ext_vector_type(N)))> : is_unsigned<T>
{
};
template <typename T, int N>
struct is_unsigned<T __attribute__((packed_vector_type(N)))> : is_unsigned<T>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_unsigned_v = is_unsigned<T>::value;
#endif

template <typename T>
struct is_enum : bool_constant<__is_enum(T)>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_enum_v = is_enum<T>::value;
#endif

template <typename T>
struct is_const : bool_constant<__is_const(T)>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_const_v = is_const<T>::value;
#endif

template <typename T>
struct _is_pointer_impl : false_type
{
};
template <typename T>
struct _is_pointer_impl<thread T*> : true_type
{
};
template <typename T>
struct _is_pointer_impl<device T*> : true_type
{
};
template <typename T>
struct _is_pointer_impl<constant T*> : true_type
{
};
template <typename T>
struct _is_pointer_impl<threadgroup T*> : true_type
{
};
#if defined(__HAVE_IMAGEBLOCKS__)
template <typename T>
struct _is_pointer_impl<threadgroup_imageblock T*> : true_type
{
};
#endif
#if defined(__HAVE_RAYTRACING__)
template <typename T>
struct _is_pointer_impl<ray_data T*> : true_type
{
};
#endif
#if defined(__HAVE_MESH__)
template <typename T>
struct _is_pointer_impl<object_data T*> : true_type
{
};
#endif
template <typename T>
struct is_pointer : _is_pointer_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_pointer_v = is_pointer<T>::value;
#endif

template <typename T>
struct _vec_elements_impl : integral_constant<size_t, 0>
{
};
template <typename T, int N>
struct _vec_elements_impl<T __attribute__((ext_vector_type(N)))> : integral_constant<size_t, N>
{
};
template <typename T, int N>
struct _vec_elements_impl<T __attribute__((packed_vector_type(N)))> : integral_constant<size_t, N>
{
};
template <typename T>
struct vec_elements : _vec_elements_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variables_templates
template <typename T>
constexpr constant size_t vec_elements_v = vec_elements<T>::value;
#endif

template <typename T>
struct _make_scalar_impl
{
  typedef T type;
};
template <typename T, int N>
struct _make_scalar_impl<T __attribute__((ext_vector_type(N)))>
{
  typedef T type;
};
template <typename T, int N>
struct _make_scalar_impl<T __attribute__((packed_vector_type(N)))>
{
  typedef T type;
};
template <typename T>
struct make_scalar : _make_scalar_impl<remove_cv_t<T>>
{
};
template <typename T>
using make_scalar_t = typename make_scalar<T>::type;

template <typename T, typename _E = void>
struct _make_signed_impl
{
};
template <typename T>
struct _make_signed_impl<T, typename enable_if<is_scalar<T>::value && is_signed<T>::value>::type>
{
  typedef T type;
};
template <>
struct _make_signed_impl<unsigned char, void>
{
  typedef signed char type;
};
template <>
struct _make_signed_impl<unsigned short, void>
{
  typedef signed short type;
};
template <>
struct _make_signed_impl<unsigned int, void>
{
  typedef signed int type;
};
template <>
struct _make_signed_impl<unsigned long, void>
{
  typedef signed long type;
};
template <typename T, int N>
struct _make_signed_impl<T __attribute__((ext_vector_type(N)))>
{
  typedef typename _make_signed_impl<T>::type __attribute__((ext_vector_type(N))) type;
};
template <typename T, int N>
struct _make_signed_impl<T __attribute__((packed_vector_type(N)))>
{
  typedef typename _make_signed_impl<T>::type __attribute__((packed_vector_type(N))) type;
};
template <typename T>
struct make_signed
{
  typedef typename _make_signed_impl<remove_cv_t<T>>::type type;
};
template <typename T>
using make_signed_t = typename make_signed<T>::type;

template <typename T, typename _E = void>
struct _make_unsigned_impl
{
};
template <typename T>
struct _make_unsigned_impl<T, typename enable_if<is_scalar<T>::value && is_unsigned<T>::value>::type>
{
  typedef T type;
};
template <>
struct _make_unsigned_impl<char, void>
{
  typedef unsigned char type;
};
template <>
struct _make_unsigned_impl<signed char, void>
{
  typedef unsigned char type;
};
template <>
struct _make_unsigned_impl<signed short, void>
{
  typedef unsigned short type;
};
template <>
struct _make_unsigned_impl<signed int, void>
{
  typedef unsigned int type;
};
template <>
struct _make_unsigned_impl<signed long, void>
{
  typedef unsigned long type;
};
template <typename T, int N>
struct _make_unsigned_impl<T __attribute__((ext_vector_type(N)))>
{
  typedef typename _make_unsigned_impl<T>::type __attribute__((ext_vector_type(N))) type;
};
template <typename T, int N>
struct _make_unsigned_impl<T __attribute__((packed_vector_type(N)))>
{
  typedef typename _make_unsigned_impl<T>::type __attribute__((packed_vector_type(N))) type;
};
template <typename T>
struct make_unsigned
{
  typedef typename _make_unsigned_impl<remove_cv_t<T>>::type type;
};
template <typename T>
using make_unsigned_t = typename make_unsigned<T>::type;

template <typename T, typename _E = void>
struct _make_bool_impl;
template <typename T>
struct _make_bool_impl<T, typename enable_if<is_arithmetic<T>::value && is_scalar<T>::value>::type>
{
  typedef bool type;
};
template <typename T, int N>
struct _make_bool_impl<T __attribute__((ext_vector_type(N)))>
{
  typedef bool __attribute__((ext_vector_type(N))) type;
};
template <typename T, int N>
struct _make_bool_impl<T __attribute__((packed_vector_type(N)))>
{
  typedef bool __attribute__((packed_vector_type(N))) type;
};
template <typename T>
struct _make_bool : _make_bool_impl<remove_cv_t<T>>
{
};
template <typename T>
using _make_bool_t = typename _make_bool<T>::type;

template <typename T>
struct underlying_type
{
  typedef __underlying_type(T) type;
};
template <typename T>
using underlying_type_t = typename underlying_type<T>::type;

template <typename... T>
struct _disjunction : false_type
{
};
template <typename T>
struct _disjunction<T> : T
{
};
template <typename T, typename... U>
struct _disjunction<T, U...> : conditional_t<bool(T::value), T, _disjunction<U...>>
{
};
#ifdef __cpp_variable_templates
template <typename... T>
using disjunction = _disjunction<T...>;
template <typename... T>
constexpr constant bool disjunction_v = disjunction<T...>::value;
#endif

template <typename... T>
struct _conjunction : true_type
{
};
template <typename T>
struct _conjunction<T> : T
{
};
template <typename T, typename... U>
struct _conjunction<T, U...> : conditional_t<bool(T::value), _conjunction<U...>, T>
{
};
#ifdef __cpp_variable_templates
template <typename... T>
using conjunction = _conjunction<T...>;
template <typename... T>
constexpr constant bool conjunction_v = conjunction<T...>::value;
#endif

template <typename T>
struct is_function : bool_constant<__is_function(T)>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_function_v = is_function<T>::value;
#endif

template <typename T>
struct _is_metal_buffer_pointee_impl : bool_constant<__is_metal_buffer_pointee(T)>
{
};
template <typename T>
struct is_metal_buffer_pointee : _is_metal_buffer_pointee_impl<remove_cv_t<T>>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_metal_buffer_pointee_v = is_metal_buffer_pointee<T>::value;
#endif

template <typename T>
struct is_metal_buffer : bool_constant<__is_metal_buffer(T)>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_metal_buffer_v = is_metal_buffer<T>::value;
#endif

template <typename T>
struct is_lvalue_reference : bool_constant<__is_lvalue_reference(T)>
{
};
#ifdef __cpp_variable_templates
template <typename T>
constexpr constant bool is_lvalue_reference_v = is_lvalue_reference<T>::value;
#endif

template <typename T> thread T &&__declval(int);
template <typename T> T   __declval(long);
template <typename T> decltype(__declval<T>(0)) declval();
} // namespace metal
#pragma METAL internals : disable

#endif // __METAL_TYPE_TRAITS
